.data

vetor: .space 40 #CRIA O VETOR DE 10 NUMEROS
newline: .asciiz " " # ESPAÇO PARA PRINTAR VALORES

.text

la $s0,vetor # CARREGA O ENDEREÇO NO REGISTRADOR s0
addi $t0,$zero,0 # CARREGA t0 COMO ZERO

read: # LABEL DO LOOP DE LER 10 NUMEROS A PARTIR DA ENTRADA DO USUARIO

slti $t1,$t0,10 # CHECA SE t0 É MENOR QUE 10
beq $t1,$zero,endread # SE t0 FOR MAIOR OU IGUAL A 10 TERMINA O LOOP
addi $v0,$zero,5 # ADICIONA EM v0 O COMANDO DE LER ENTRADA
syscall # CHAMA O SISTEMA

sll $t2,$t0,2 # MULTIPLICA O VATOR DE t0 POR 4 PARA CALCULAR A POSIÇÃO DE ARMAZENAMENTO EM s0
add $t3,$t2,$s0 # ADICIONA A MULTIPLICAÇÃO DE CIMA COM O ENDEREÇO DE s0 PARA PODER ATUALIZAR A POSIÇÃO EM QUESTAO

sw $v0,0($t3) # CARREGA O VALOR DIGITADO PELO USUARIO NO ENDEREÇO EM QUESTÃO DE s0
addi $t0,$t0,1 # INCREMENTE O CONTADOR
j read

endread:

# t0 = K
# t1 = J

addi $t0,$zero,1 # CARREGA 1 EM t0

for1: # FOR NUMERO 1
slti $t3,$t0,10 # CHECA SE t0 É MENOR QUE 10 E SALVA EM t3
beq $t3,$zero,endfor1 # SE t0 FOR MAIOR OU IGUAL A 10 TERMINE O FOR 1
addi $t1,$zero,0 # ADICIONA 0 EM t1 QUE É O INCREMENTADOR DO SEGUNDO FOR
j for2 # PULA PARA O FOR 2
for2: # INICIA O FOR 2
slti $t4,$t1,9 # CHECA SE t1 É MENOR QUE 9
beq $t4,$zero,endfor2 # SE FOR MAIOR OU IGUAL A 9 TERMINE O FOR 2

sll $t3,$t1,2 # SHIFT LEFT PARA CALCULAR O ENDEREÇO DE MEMORIA DE s0 NA POSIÇÃO J
add $t4,$t3,$s0 # ENDEREÇO DE MEMORIA DE s0 NA POSIÇÃO J

lw $t3,0($t4) # VALOR DE s0 NA POSIÇÃO J

addi $t5,$t1,1 # INCREMENTA J+1
sll $t6,$t5,2 # SHIFT LEFT PARA CALCULAR O ENDEREÇO DE MEMORIA DE s0 NA POSIÇÃO J+1
add $t7,$t6,$s0 # ENDEREÇO DE MEMORIA DE s0 NA POSIÇÃO J+1

lw $t6,0($t7) #VALOR DE s0 NA POSIÇÃO J+1

slt $t5,$t6,$t3 # IF DO BUBBLE SORT / CHECA SE O VALOR DE s0 NA POSIÇÃO J+1 É MENOR QUE O VALOR DE s0 NA POSIÇÃO J
beq $t5,$zero,endif # SE FOR MAIOR NÃO REALIZA O IF

add $t2,$zero,$t3 # ADICIONA O VALOR DE s0 NA POSIÇÃO J AO AUX
sw $t6,0($t4) # s0 NA POSIÇÃO J RECEBE s0 NA POSIÇÃO J+1
sw $t2,0($t7) # s0 NA POSIÇÃO J+1 RECEBE AUX


addi $t1,$t1,1 # INCREMENTA O t1 QUE É J
j for2 # VOLTA PRO FOR 2
 
endif: # SE NÃO FIZER O IF
addi $t1,$t1,1 # INCREMENTA O t1 QUE É J
j for2 # VOLTA PRO FOR 2

endfor2: # FIM DO FOR 2
addi $t0,$t0,1 # INCREMENTA 0 t0 QUE É K
j for1 # VOLTA PRO FOR 1

endfor1: # FIM DO FOR 1


# AQUI EMBAIXO É SO PRA PRINTAR

addi $t0,$zero,0 # ZERA t0
Loop2: # LOOP DO PRINT
slti $t1,$t0,10 # CHECA SE É MENOR QUE 10
beq $t1,$zero,endloop # SE FOR MAIOR OU IGUAL TERMINA
sll $t2,$t0,2 # CALCULA O SHIFT LEFT PRO ENDEREÇO 
add $t3,$t2,$s0 # ADICIONA O VALOR DO SHIFT AO ENDEREÇO DE s0
lw $t4,0($t3) # CARREGA O VALOR EM t4
li $v0,1 # COMANDO DE PRINT INT
move $a0,$t4 # MOVE O VALOR PRA a0
syscall # PRINTA
li $v0, 4 # COMANDO DE PRINT DE STRING
la $a0, newline # CARREGA A STRING EM a0
syscall # PRINTA
addi $t0,$t0,1 # INCREMENTA O ITERADOR 
j Loop2 # VOLTA PRO LOOP

endloop: # TERMINA LOOP